plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

group = 'ink.magma'
version = '1.2.5'

repositories {
    maven { url 'https://maven.aliyun.com/repository/central' }
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = "https://repo.william278.net/releases" }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT"
    compileOnly 'net.md-5:bungeecord-api:1.19-R0.1-SNAPSHOT'

    implementation "com.github.Revxrsal.Lamp:common:3.2.0"
    implementation "com.github.Revxrsal.Lamp:bukkit:3.2.0"
    implementation "com.github.Revxrsal.Lamp:bungee:3.2.0"
    implementation "net.kyori:adventure-api:4.16.0"
    implementation "net.kyori:adventure-text-minimessage:4.16.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.2"
    implementation "net.william278:annotaml:2.0.7"
    implementation "redis.clients:jedis:5.1.2"

    compileOnly 'net.william278.huskhomes:huskhomes-bukkit:4.6.2'
}


// Java 版本
def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}


// UTF-8
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

// plugin.yml 变量赋值
processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    filesMatching('bungee.yml') {
        expand props
    }
}


import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

tasks.named('shadowJar', ShadowJar) {
    enableRelocation true
    relocationPrefix "ink.magma.zthpatches.libraries"
    minimize()
}